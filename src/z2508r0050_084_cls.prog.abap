*&---------------------------------------------------------------------*
*& Include          Z2508R0040_084_CLS
*&---------------------------------------------------------------------*
CLASS LCL_EVENT_HANDLER DEFINITION.
  PUBLIC SECTION.

    CLASS-METHODS ON_HOTSPOT_CLICK
      FOR EVENT HOTSPOT_CLICK OF CL_GUI_ALV_GRID
      IMPORTING E_ROW_ID E_COLUMN_ID.

    CLASS-METHODS ON_BUTTON_CLICK FOR EVENT BUTTON_CLICK OF CL_GUI_ALV_GRID
      IMPORTING ES_ROW_NO ES_COL_ID.

    CLASS-METHODS ON_TOOLBAR1 FOR EVENT TOOLBAR OF CL_GUI_ALV_GRID IMPORTING E_OBJECT.
    CLASS-METHODS ON_TOOLBAR2 FOR EVENT TOOLBAR OF CL_GUI_ALV_GRID IMPORTING E_OBJECT.

    CLASS-METHODS ON_USER_COMMAND FOR EVENT USER_COMMAND OF CL_GUI_ALV_GRID
      IMPORTING E_UCOMM.

    CLASS-METHODS ON_DATA_CHANGED FOR EVENT DATA_CHANGED OF CL_GUI_ALV_GRID
      IMPORTING ER_DATA_CHANGED.

    CLASS-METHODS ON_DOUBLE_CLICK FOR EVENT DOUBLE_CLICK OF CL_GUI_ALV_GRID
            IMPORTING E_ROW E_COLUMN .

ENDCLASS.
*&---------------------------------------------------------------------*
*& Class (Implementation) LCL_EVENT_HANDLER
*&---------------------------------------------------------------------*
*&
*&---------------------------------------------------------------------*
CLASS LCL_EVENT_HANDLER IMPLEMENTATION.
  METHOD ON_HOTSPOT_CLICK.
    CLEAR GS_DATA.

    CASE E_COLUMN_ID-FIELDNAME.
      WHEN 'EBELN'.
        READ TABLE GT_DATA INTO GS_DATA INDEX E_ROW_ID-INDEX.
        IF SY-SUBRC = 0.
          SET PARAMETER ID 'BES' FIELD GS_DATA-EBELN.
          CALL TRANSACTION 'ME23N' AND SKIP FIRST SCREEN.
        ENDIF.

" 아이템 수정 후 BAPI 오류메시지 팝업
      WHEN 'MESSAGE'.
        READ TABLE GT_ITEM ASSIGNING FIELD-SYMBOL(<FS_MSG>) INDEX E_ROW_ID-INDEX.
        CALL FUNCTION 'RSCRMBW_DISPLAY_BAPIRET2'
          TABLES
            IT_RETURN = <FS_MSG>-RETURN_MSG.

    ENDCASE.

  ENDMETHOD.

  METHOD ON_BUTTON_CLICK.
    READ TABLE GT_HEADER ASSIGNING FIELD-SYMBOL(<FS_HEADER>) INDEX ES_ROW_NO-ROW_ID.

* 헤더 버튼 눌렀을 때 아이템 조회
    IF ES_COL_ID-FIELDNAME = 'BTN_ICON'.
      PERFORM SELECT_ITEM USING <FS_HEADER> ES_ROW_NO-ROW_ID.
    ENDIF.

  ENDMETHOD.

* 헤더 툴바 새로고침
  METHOD ON_TOOLBAR1.
    DATA: LS_TOOLBAR TYPE STB_BUTTON.

    CLEAR LS_TOOLBAR.
    LS_TOOLBAR-BUTN_TYPE = 3.
    APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.

    CLEAR LS_TOOLBAR.
    LS_TOOLBAR-FUNCTION = 'REFRESH'.
    LS_TOOLBAR-ICON = ICON_REFRESH.
    LS_TOOLBAR-TEXT = '새로고침'.

    APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.

  ENDMETHOD.

* 아이템 툴바 SELECT
  METHOD ON_TOOLBAR2.
    DATA LS_TOOLBAR TYPE STB_BUTTON.

    CLEAR LS_TOOLBAR.
    LS_TOOLBAR-BUTN_TYPE = 3.
    APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.

    CLEAR LS_TOOLBAR.
    LS_TOOLBAR-FUNCTION = 'SELECT_ALL'.
    LS_TOOLBAR-ICON = ICON_SELECT_ALL.
    LS_TOOLBAR-TEXT = '전체선택'.
    APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.

    CLEAR LS_TOOLBAR.
    LS_TOOLBAR-FUNCTION = 'DESELECT_ALL'.
    LS_TOOLBAR-ICON = ICON_DESELECT_ALL.
    LS_TOOLBAR-TEXT = '전체해제'.
    APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.

    CLEAR LS_TOOLBAR.
    LS_TOOLBAR-FUNCTION = 'SELECT_BLOCK'.
    LS_TOOLBAR-ICON = ICON_SELECT_BLOCK.
    LS_TOOLBAR-TEXT = '지정선택'.
    APPEND LS_TOOLBAR TO E_OBJECT->MT_TOOLBAR.

  ENDMETHOD.

* 툴바 버튼에 대한 동작
  METHOD ON_USER_COMMAND.

    CASE E_UCOMM.
      WHEN 'REFRESH'.
        LOOP AT GT_HEADER ASSIGNING FIELD-SYMBOL(<FS_HEADER>).
          <FS_HEADER>-BTN_ICON = ICON_ENTER_MORE. " 기본값
        ENDLOOP.

        REFRESH GT_ITEM.
        PERFORM REFRESH_ALV_0200.

      WHEN 'SELECT_ALL'.
        LOOP AT GT_ITEM ASSIGNING FIELD-SYMBOL(<FS_ITEM>).
          CHECK <FS_ITEM>-STATUS = ICON_LED_YELLOW.
          <FS_ITEM>-CHECK = ABAP_TRUE.
        ENDLOOP.
        GO_ALV_GRID2->REFRESH_TABLE_DISPLAY( ).


      WHEN 'DESELECT_ALL'.
        LOOP AT GT_ITEM ASSIGNING <FS_ITEM>.
          <FS_ITEM>-CHECK = ABAP_FALSE.
        ENDLOOP.
        GO_ALV_GRID2->REFRESH_TABLE_DISPLAY( ).


      WHEN 'SELECT_BLOCK'.
        DATA : LT_INDEX_ROWS TYPE LVC_T_ROW,
               LS_INDEX_ROWS TYPE LVC_S_ROW.

        CALL METHOD GO_ALV_GRID2->GET_SELECTED_ROWS
          IMPORTING
            ET_INDEX_ROWS = LT_INDEX_ROWS.                 " Indexes of Selected Rows
        LOOP AT LT_INDEX_ROWS INTO LS_INDEX_ROWS.
          READ TABLE GT_ITEM ASSIGNING <FS_ITEM> INDEX LS_INDEX_ROWS-INDEX.
          IF SY-SUBRC = 0 AND <FS_ITEM>-STATUS = ICON_LED_YELLOW.
            <FS_ITEM>-CHECK = ABAP_TRUE.
          ENDIF.
        ENDLOOP.

        GO_ALV_GRID2->REFRESH_TABLE_DISPLAY( ).
    ENDCASE.

  ENDMETHOD.

* ALV 필드 수정에 따른 동작
  METHOD ON_DATA_CHANGED.

    DATA: LS_MOD_CELLS TYPE LVC_S_MODI,
          LV_MENGE     LIKE GS_ITEM-MENGE,
          LV_NETPR     LIKE GS_ITEM-NETPR,
          LV_EINDT     LIKE GS_ITEM-EINDT.

    PERFORM MODIFY_ITEM USING :
         ER_DATA_CHANGED 'MENGE' LV_MENGE,
         ER_DATA_CHANGED 'NETPR' LV_NETPR,
         ER_DATA_CHANGED 'EINDT' LV_EINDT.


  ENDMETHOD.

* PO 번호 더블클릭 이벤트
  METHOD ON_DOUBLE_CLICK.
    READ TABLE GT_ITEM ASSIGNING FIELD-SYMBOL(<FS_ITEM>) INDEX E_ROW-INDEX.
    IF SY-SUBRC = 0.
      CHECK E_COLUMN-FIELDNAME = 'EBELN'.
      SET PARAMETER ID 'BES' FIELD <FS_ITEM>-EBELN.
      CALL TRANSACTION 'ME23N' AND SKIP FIRST SCREEN.
    ENDIF.
    ENDMETHOD.
ENDCLASS.
